class FW_Revolver : FollowerWeapon
{
	int Chamber[6];
	override string GetName() { return "Revolver"; }
	override string GetIcon() { return "REVLA0"; }
	override String, class<HDMagAmmo>, int GetAmmoType() { return 'HDRevolverAmmo HDPistolAmmo', null, 6; }
	override Name GetRealWeapon() { return 'HDRevolver'; }
	
	Void RotateCylinder()
	{
		int cylbak=Chamber[0];
		Chamber[0]=Chamber[5];
		Chamber[5]=Chamber[4];
		Chamber[4]=Chamber[3];
		Chamber[3]=Chamber[2];
		Chamber[2]=Chamber[1];
		Chamber[1]=cylbak;
	}
	
	override FireSequenceResult DoFiringSequence(out int ticker, Actor trgt)
	{
		switch (ticker) {
			case 0: {
				Owner.A_StartSound("weapons/deinocyl",CHAN_WEAPON);
			}
			case 4: {
			
				if (Mag < 1)
				{
					return FSResult_End;
				}
				
				RotateCylinder();
				
				if(Chamber[0] != BUGS_NINEMIL && Chamber[0]!= BUGS_MASTERBALL){
					Owner.A_StartSound("weapons/deinoclick",8,CHANF_OVERLAP);
					ticker = 6;
					Break;
				}

				TryChamberRound();
				
				Owner.tics++;
				Owner.frame++; // [Ace] Muzzle flash frame.
				Owner.A_StartSound("weapons/deinoblast1",CHAN_WEAPON,CHANF_OVERLAP,0.5);
				Owner.A_StartSound("weapons/deinoblast2",CHAN_WEAPON,CHANF_OVERLAP,0.4);
				HDBulletActor.FireBullet(Owner, (Chamber[0]==BUGS_NINEMIL)?"HDB_9":"HDB_355", Owner.GunHeight, speedfactor: frandom(0.97,1.03) );
				ChamberedRound = CRType_Empty;
				
				Chamber[0]=(Chamber[0]==BUGS_NINEMIL)?BUGS_NINEMILSPENT:BUGS_MASTERBALLSPENT;
				break;
			}
			case 8:
			{
				ticker = -1;
				break;
			}
		}
		return FSResult_None;
	}
	override bool DoReloadSequence(out int ticker)
	{
		switch (ticker)
		{
			case 1:
			{
				Owner.A_StartSound("weapons/deinoopen", 8);
			}
			case 4:
			{
				Owner.A_StartSound("weapons/deinoeject", 8);
				For(int i=0;i<6;i++){
					Switch(Chamber[i])
					{
						Case BUGS_NINEMILSPENT:
							Chamber[i] = 0;
							Owner.A_SpawnItemEx("HDSpent9mm", cos(Owner.pitch) * 4, 0, Owner.height - 10 - sin(Owner.pitch) * 4, Owner.vel.x, Owner.vel.y, Owner.vel.z + frandom(2, 3), frandom(-1, 1), SXF_ABSOLUTEMOMENTUM | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH );
							Break;
						Case BUGS_NINEMIL:
							Chamber[i] = 0;
							InventorySlot NineMil = Owner.FindLowestAmount('HDPistolAmmo');
							if(NineMil)NineMil.Amount++;
							Mag--;
							Break;
						Case BUGS_MASTERBALLSPENT:
							Chamber[i] = 0;
							Owner.A_SpawnItemEx("HDSpent355", cos(Owner.pitch) * 4, 0, Owner.height - 10 - sin(Owner.pitch) * 4, Owner.vel.x, Owner.vel.y, Owner.vel.z + frandom(2, 3), frandom(-1, 1), SXF_ABSOLUTEMOMENTUM | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH );
							Break;
					}
				}
				break;
			}
			case 22:
			{
				if (TryReload() == RResult_CanReload)
				{
					Owner.A_StartSound("weapons/deinoload", 9, CHANF_OVERLAP);
					ticker = 17;
				}
				break;
			}
			case 26:
			{
				Owner.A_StartSound("weapons/deinoclose", 8);
			}
			case 27:
			{
				return FSResult_End;
			}
		}
		return FSResult_None;
	}
	override int AssessPriority(Actor trgt, int shields, bool multitarget)
	{
		if (trgt is 'PlayerPawn')
		{
			return WPriority_VeryLow;
		}
		if (trgt.bBOSS || trgt is 'PainLord')
		{
			return WPriority_Lowest;
		}
		if (trgt is 'Necromancer' && !multitarget)
		{
			return WPriority_VeryLow;
		}
		if (trgt is 'PainBringer' || shields > 300)
		{
			return WPriority_Medium;
		}
		return WPriority_Lowest;
	}

	override void OnTransfer(HDWeapon wpn)
	{
		ChamberedRound = CRType_Empty;
		for (int i = BUGS_CYL1; i <= BUGS_CYL6; i++)
		{
			Chamber[i-1] = wpn.WeaponStatus[i];
			if(Chamber[i-1] == BUGS_MASTERBALL || Chamber[i-1] == BUGS_NINEMIL)Mag++;
		}
	}
	
	override void OnBackTransfer(HDWeapon wpn)
	{
		for (int i = BUGS_CYL1; i <= BUGS_CYL6; i++)
		{
			wpn.WeaponStatus[i] = Chamber[i-1];
		}
	}
	
	Override Void EmptyWeapon()
	{
		class<HDAmmo> AType, AType2;
		String Ammos = GetAmmoType();
		
		AType = Ammos.Left(Ammos.IndexOf(" "));
		AType2 = Ammos.Mid(Ammos.IndexOf(" ")+1);

		For(int i=0;i<6;i++){
			Switch(Chamber[i])
			{
				Case BUGS_NINEMILSPENT:
					Owner.A_SpawnItemEx("HDSpent9mm", random(0, 16), 0, 0, frandom(1.0, 2.0), 0, frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
					Break;
				Case BUGS_NINEMIL:
					Owner.A_SpawnItemEx("HDPistolAmmo", random(0, 16), 0, 0, frandom(1.0, 2.0), 0, frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
					Break;
				Case BUGS_MASTERBALLSPENT:
					Owner.A_SpawnItemEx("HDSpent355", random(0, 16), 0, 0, frandom(1.0, 2.0), 0, frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
					Break;
				Case BUGS_MASTERBALL:
					Owner.A_SpawnItemEx("HDRevolverAmmo", random(0, 16), 0, 0, frandom(1.0, 2.0), 0, frandom(2.0, 5.0), 0, SXF_NOCHECKPOSITION);
			}
			Chamber[i] = 0;
		}
		Mag = MagRecast = 0;
	}
	
	Override ReloadResult TryReload(int flags)
	{
		class<HDAmmo> AType, AType2;
		String Ammos = GetAmmoType();
		
		AType = Ammos.Left(Ammos.IndexOf(" "));
		AType2 = Ammos.Mid(Ammos.IndexOf(" ")+1);
				
		if(Mag >= 6)
		{
			return RResult_MagFull;
		}
		if(Owner.GetAmount(AType) == 0 && Owner.GetAmount(AType2) == 0)
		{
			return RResult_NoAmmo;
		}
		if(flags & TRF_CHECK)
		{
			return RResult_CanReload;
		}
		
		For(int i=0;i<6;i++)
		{
			if(Chamber[i]!=0)Continue;
			
			Class<HDAmmo> Loading = AType;
			if(Owner.GetAmount(Loading)<1){
				Loading = AType2;
				Chamber[i] = BUGS_NINEMIL;
			}
			else Chamber[i] = BUGS_MASTERBALL;
						
			InventorySlot wpnAmmo = Owner.FindLowestAmount(Loading);
			Mag++;
			wpnAmmo.Amount--;
			if(wpnAmmo.Amount == 0)
			{
				Owner.TakeInvItem(wpnAmmo, HDFollower.FIF_FORCE | HDFollower.FIF_REMOVE);
			}
			
			Break;
		}

		return RResult_CanReload;
	}
}